#include <SoftwareSerial.h>
#define RX 2
#define TX 3
SoftwareSerial esp(RX, TX);

const int sensorHallPin = 4;

String WIFI_SSID = "Gabriel";
String WIFI_PASS = "03800442";

String THINGSPEAK_HOST = "api.thingspeak.com";
String WRITE_API_KEY = "QWJMV44QXFXL5ONY";

// Controle do tempo de ausência
unsigned long ausenciaInicio = 0;
bool imaAusente = false;
const unsigned long tempoLimite = 1000; // 1 segundo
bool mensagemEnviada = false;

void setup() {
  Serial.begin(9600);
  esp.begin(115200);

  Serial.println("Arduino iniciado"); // <-- Mensagem para Python ao ligar

  pinMode(sensorHallPin, INPUT);

  enviarAT("AT", 2000);
  enviarAT("AT+RST", 5000);
  delay(5000);
  enviarAT("AT+CWMODE=1", 2000);

  // ---------- Tenta conectar ao WiFi ----------
  String resp = enviarAT("AT+CWJAP=\"" + WIFI_SSID + "\",\"" + WIFI_PASS + "\"", 15000);

  if (resp.indexOf("WIFI CONNECTED") != -1 || resp.indexOf("OK") != -1) {
      Serial.println("Wifi conectado com sucesso");  // <-- Mensagem para Python
  } else {
      Serial.println("Falha ao conectar no WiFi");    // <-- Mensagem para Python
  }
  // ------------------------------------------------

  pinMode(5, OUTPUT);
  digitalWrite(5, HIGH);  
}

void loop() {
  int estadoSensor = digitalRead(sensorHallPin);

  if (estadoSensor == HIGH) {  // Ima AUSENTE
    if (!imaAusente) {
      ausenciaInicio = millis();
      imaAusente = true;
      mensagemEnviada = false;
    } else {
      if (!mensagemEnviada && (millis() - ausenciaInicio >= tempoLimite)) {
        Serial.println("Ima ausente por 1 segundo! Enviando mensagem...");
        if (enviarMensagemThingSpeak("10")) {
          Serial.println("Mensagem enviada!");
        } else {
          Serial.println("Erro ao enviar.");
        }
        mensagemEnviada = true;
      }
    }
  } 
  else { // Ima PRESENTE
    imaAusente = false;
    mensagemEnviada = false;
  }

  // Ler respostas do ESP
  while (esp.available()) {
    Serial.write(esp.read());
  }
}

// --------------------------------------------------
// Função: Envia valor para o ThingSpeak
// --------------------------------------------------
bool enviarMensagemThingSpeak(String mensagem) {
  Serial.println("Conectando ao ThingSpeak...");

  String resp = enviarAT("AT+CIPSTART=\"TCP\",\"" + THINGSPEAK_HOST + "\",80", 5000);
  if (resp.indexOf("ERROR") != -1) return false;

  String url = "/update?api_key=" + WRITE_API_KEY + "&field1=" + urlencode(mensagem);
  String req = "GET " + url + " HTTP/1.1\r\nHost: " + THINGSPEAK_HOST + "\r\nConnection: close\r\n\r\n";

  enviarAT("AT+CIPSEND=" + String(req.length()), 3000);
  delay(500);
  esp.print(req);

  long t0 = millis();
  String response = "";
  while (millis() - t0 < 5000) {
    while (esp.available()) response += (char)esp.read();
  }

  enviarAT("AT+CIPCLOSE", 3000);

  return (response.indexOf("200 OK") != -1);
}

// --------------------------------------------------
// Funções auxiliares
// --------------------------------------------------

String enviarAT(String cmd, uint32_t timeout) {
  esp.print(cmd + "\r\n");
  long t0 = millis();
  String resp = "";
  while (millis() - t0 < timeout) {
    while (esp.available()) resp += (char)esp.read();
  }
  
  return resp;
}

String urlencode(String str) {
  String encoded = "";
  for (int i = 0; i < str.length(); i++) {
    char c = str.charAt(i);
    if (isalnum(c)) encoded += c;
    else if (c == ' ') encoded += "%20";
    else {
      char hex[5];
      sprintf(hex, "%%%02X", c);
      encoded += hex;
    }
  }
  return encoded;
}
